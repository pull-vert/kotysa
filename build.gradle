plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50' apply false
    id 'org.jetbrains.dokka' version '0.9.18' apply false
    id 'com.jfrog.bintray' version '1.8.4' apply false
    id 'net.researchgate.release' version '2.8.1'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE' apply false
}

println("Using Gradle version: $gradle.gradleVersion")

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    if (name != "kotysa-test-common") {
        apply plugin: 'maven-publish'
        apply plugin: 'com.jfrog.bintray'
    }

    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/michaelbull/maven' }
    }

    dependencyManagement {
        imports {
            mavenBom "org.junit:junit-bom:$junit_bom_version"
            mavenBom "org.jetbrains.kotlinx:kotlinx-coroutines-bom:$kotlinx_coroutines_bom_version"
        }
    }

    jacoco {
        toolVersion = "0.8.5"
    }

    jacocoTestReport {
        dependsOn(test)

        reports {
            html.enabled = true
            xml.enabled = true
        }
    }
}

// Workaround for https://github.com/researchgate/gradle-release/issues/144
task build {
    dependsOn subprojects.findResults { it.tasks.findByName('build') }
}

// execute ./gradlew wrapper then remove .gradle directory when version change
wrapper {
    gradleVersion = '5.6.3'
    distributionType = Wrapper.DistributionType.ALL
}
