plugins {
    id 'org.jetbrains.kotlin.jvm' apply false
    id 'org.jetbrains.dokka' apply false
    id 'com.jfrog.bintray' apply false
    id 'net.researchgate.release'
}

println("Using Gradle version: $gradle.gradleVersion")

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'jacoco'

    if (name != "kotysa-test-common") {
        apply plugin: 'maven-publish'
        apply plugin: 'com.jfrog.bintray'
    }

    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/michaelbull/maven' }
    }

    dependencies {
        // import BOM
        implementation platform("org.junit:junit-bom:$junit_bom_version")
        implementation platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:$kotlinx_coroutines_bom_version")
    }

    jacoco {
        toolVersion = "0.8.5"
    }

    tasks.withType(Test) {
        jacoco.includeNoLocationClasses = true
    }

    jacocoTestReport {
        dependsOn(test)

        reports {
            html.enabled = true
            xml.enabled = true
        }
    }
}

// Workaround for https://github.com/researchgate/gradle-release/issues/144
task build {
    dependsOn subprojects.findResults { it.tasks.findByName('build') }
}

// execute ./gradlew wrapper then remove .gradle directory when version change
wrapper {
    gradleVersion = '5.6.4'
    distributionType = Wrapper.DistributionType.ALL
}
